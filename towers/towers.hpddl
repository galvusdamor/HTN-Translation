(define (domain towers)
 (:requirements :strips :disjunctive-preconditions)
 (:types OBJ RING - OBJ TOWER - OBJ)
 (:predicates
  (on ?r - RING ?o - OBJ)
  (towerTop ?o - OBJ ?t - TOWER)
  (smallerThan ?r - RING ?o - OBJ))

 (:method shiftTower
  ; Shift tower from ?t1 to ?t3 using ?t2
  :parameters (?t1 - TOWER ?t2 - TOWER ?t3 - TOWER)
  :task (shiftTower ?t1 ?t2 ?t3)
  :branches (
   (:branch done
    :precondition (and (towerTop ?t1 ?t1) (towerTop ?t3 ?t3))
    :tasks ())
   (:branch working
    :tasks (
     (exchange ?t1 ?t2) ; smallest
     (exchange ?t1 ?t3)
     (exchange ?t2 ?t3) ; smallest
     (exchange ?t1 ?t2)
     (exchange ?t3 ?t1) ; smallest
     (exchange ?t2 ?t3)
     (shiftTower ?t1 ?t2 ?t3)))))

 (:method exchangelr
  :parameters (?t1 - TOWER ?t2 - TOWER ?r1 - RING ?o2 - OBJ)
  :task (exchange ?t1 ?t2)
  :precondition
  (and
    (smallerThan ?r1 ?o2)
    (towerTop ?r1 ?t1)
    (towerTop ?o2 ?t2))
  :branches ((:branch d :tasks ((move ?t1 ?t2)))))

 (:method exchange
  :parameters (?t1 - TOWER ?t2 - TOWER ?r1 - RING ?o1 - OBJ ?o2 - OBJ)
  :task (exchange ?t1 ?t2)
  :precondition
  (and
    (towerTop ?r1 ?t2)
    (towerTop ?o2 ?t1)
    (smallerThan ?r1 ?o2))
  :branches ((:branch d :tasks ((move ?t2 ?t1)))))

 (:action move
  :parameters (?r - RING ?o1 - OBJ ?t1 - TOWER ?o2 - OBJ ?t2 - TOWER)
  :task (move ?t1 ?t2)
  :precondition (and
    (on ?r ?o1) (towerTop ?r ?t1)
    (towerTop ?o2 ?t2) (smallerThan ?r ?o2))
  :effect (and
    (not (on ?r ?o1)) 
    (on ?r ?o2)
    (not (towerTop ?r ?t1))
    (towerTop ?o1 ?t1) 
    (not (towerTop ?o2 ?t2))
    (towerTop ?r ?t2))))


